//controls and game rules
//press left key to move left and right key to move right
//press space to shoot
//miss 5 enemies and you lose

class Main
{
    function void main()
    {
        var SpaceShip ss;
        var int k;

        var int x_ammo,y_ammo;
        var int y_bound_add_left,y_bound_add_right;

        var Enemy e;
        var int max_enemy;
        var int enemy_creation_cycle;


        var int z;

        var int score;

        do Screen.drawLine(127,0,127,256);
        do Screen.drawLine(384,0,384,256);

        let y_bound_add_left = 127/16; // calculates y axis address bounds.
        let y_bound_add_right = 384/16;

        let ss = SpaceShip.new(239,16,y_bound_add_left,y_bound_add_right);

        let max_enemy = 100;

        let e = Enemy.new(y_bound_add_left,y_bound_add_right,max_enemy);

        let z = 0;

        let enemy_creation_cycle = 20;

        
        do Output.moveCursor(5,1);

        do Output.printString("Score:");
        do ss.draw(); // draw the intial  space ship
        while(~(e.game_over()))
        {

            do Output.printInt(e.return_enemy_kill());
            
            let k = Keyboard.keyPressed();
            if(Random.mod(z,enemy_creation_cycle) = 0)
            {
                do e.create_new();
            }
            
            if(k = 130)
            {
                do ss.move_left();
            }
            if (k = 132)
            {
                do ss.move_right();
            }
            if(k = 32)
            {
                let x_ammo = ss.return_x() - 16;
                let y_ammo = ss.return_y();
                do ss.trigger(x_ammo,y_ammo);
            }
            
            
            do e.update_enemy(); //updates the position of enemy
            do ss.fire_update(e); // update the position of bullet
            let z = z + 1;
            do Sys.wait(100);
            do Output.moveCursor(5,7);
        }


        do Output.printInt(e.return_enemy_kill());
        do Output.moveCursor(6,1);

        do Output.printString("Game Over");

        
        return;
    }
}